# Change these to suit your project, if you want.
project(
  'dino-game', 'c',
  version: '0.90.0',
  license: 'Zlib',
  meson_version: '>=0.60.0',
  default_options: ['buildtype=debugoptimized', 'warning_level=2', 'c_std=c11', 'cpp_std=c++20'])

# Set up recommended compiler and linker flags.
subdir('meson/toolchain')
# If you're working on meson-gba
if get_option('dev')
  subdir('meson/dev')
  subdir_done()
endif

# The name of your project.
name = 'dino-game'

# All of the source files used by your project.
sources = [
  'src/input.c',
  'src/text.c',
  'src/kinematics.c',
  'src/object_utils.c',
  'src/system_utils.c',
  'src/main.c',
  'src/assets/blob.s',
  'src/assets/bg_with_road.s',
  'src/assets/aero_fly.s',
  'src/assets/aero_single.s',
  'src/assets/biplup_walk.s',
  'src/assets/cactus.s',
  'src/assets/cloud.s',
  'src/assets/dactyl_flying.s',
  'src/assets/dino_walk.s',
  'src/assets/sudowoodo.s',
  'src/assets/dactyl_single.s'
]

# All of the include directories used by your project.
includes = [
  'include',
]

# Dependencies. gba-meson provides a large number of GBA development libraries.
# Check them out and add the ones you want to use to this list.
dependencies = [
  dependency('minrt'),
  dependency('libtonc')
]

# The main executable for your game. We give it a '.elf' file extension,
# just to make the file more easy to differentiate from a raw ROM.
elf = executable(
  name,
  sources,
  include_directories: includes,
  dependencies: dependencies,
  name_suffix: 'elf'
)

# Get the 'makerom' program from sdk-seven.
makerom = find_program('makerom')

# Build a raw GBA ROM file from your ELF file.
rom = custom_target(
  name + '-rom',
  input: elf,
  output: name + '.gba',
  command: [makerom, '@INPUT@', '@OUTPUT@'],
  build_by_default: true
)
